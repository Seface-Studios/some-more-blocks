plugins {
  id "fabric-loom" version "${fabric_loom_version}"
  id "com.github.node-gradle.node" version "3.5.0"
}

node {
  version = '20.0.0'
  npmVersion = '10.0.0'
  download = true
}

dependencies {
  minecraft "com.mojang:minecraft:${minecraft_version}"
  mappings loom.officialMojangMappings()
  modImplementation "net.fabricmc:fabric-loader:${fabric_loader_version}"
  modImplementation "net.fabricmc.fabric-api:fabric-api:${fabric_version}"
}

fabricApi {
  configureDataGeneration((settings) -> {
    settings.client = true
    settings.getOutputDirectory().set(project(":common").file("src/main/generated"))
  })
}

loom {
  if (project(":common").file("src/main/resources/${mod_id}.accesswidener").exists()) {
    accessWidenerPath.set(project(":common").file("src/main/resources/${mod_id}.accesswidener"))
  }

  mixin {
    defaultRefmapName.set("${mod_id}.refmap.json")
  }

  runs {
    client {
      client()
      setConfigName("Fabric Client")
      ideConfigGenerated(true)
      runDir("run")
    }

    server {
      server()
      setConfigName("Fabric Server")
      ideConfigGenerated(true)
      runDir("run")
    }
  }
}

task applyLocalizations(type: NodeTask) {
  script = project(":common").file('../scripts/langfy/main.mjs')
  args = [minecraft_version, mod_name]
}

tasks.named("runDatagen") {
  finalizedBy tasks.named("applyLocalizations")
}
