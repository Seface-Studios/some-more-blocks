plugins {
  id "java-library"
  id "net.neoforged.moddev" version "${neoforge_loom_version}"
}

repositories {
  mavenLocal()
}

dependencies {
  implementation "net.neoforged:neoforge:${neoforge_version}"
}

neoForge {
  version = neoforge_version

  var File AT = project(":common").file('src/main/resources/META-INF/accesstransformer.cfg')
  if (AT.exists()) {
    accessTransformers.from(AT.absolutePath)
  }

  parchment {
    mappingsVersion = neoforge_mappings_version
    minecraftVersion = minecraft_version
  }

  runs {
    configureEach {
      systemProperty('neoforge.enabledGameTestNamespaces', mod_id)
      ideName = "NeoForge ${it.name.capitalize()} (${project.path})"
    }

    client {
      client()
    }

    server {
      server()
      programArgument '--nogui'
    }
  }

  mods {
    "${mod_id}" {
      sourceSet(sourceSets.main)
    }
  }
}

sourceSets.each {
  def dir = layout.buildDirectory.dir("sourcesSets/$it.name")
  it.output.resourcesDir = dir
  it.java.destinationDirectory = dir
}

idea {
  module {
    downloadSources = true
    downloadJavadoc = true
  }
}
