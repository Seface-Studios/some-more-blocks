plugins {
  id "net.minecraftforge.gradle" version "${forge_loom_version}"
  id "org.spongepowered.mixin" version "0.7-SNAPSHOT"
}

jar {
  manifest {
    attributes(
      'MixinConfigs'            : "${mod_id}.mixins.json,${mod_id}.forge.mixins.json"
    )
  }
}

minecraft {
  mappings channel: 'official', version: minecraft_version
  reobf = false
  copyIdeResources = true

  var File AT = project(":forge").file('src/main/resources/META-INF/accesstransformer.cfg')
  if (AT.exists()) {
    accessTransformers.from(AT.absolutePath)
  }

  runs {
    configureEach {
      workingDirectory project.file('run')
      ideaModule "${rootProject.name}.${project.name}.main"
      property 'mixin.env.remapRefMap', 'true'
      property 'mixin.env.refMapRemappingFile', "${projectDir}/build/createSrgToMcp/output.srg"
      taskName "${it.name.capitalize()}"
    }

    client {
      mods {
        modClientRun {
          source sourceSets.main
          source project(":common").sourceSets.main
        }
      }
    }

    server {
      mods {
        modServerRun {
          source sourceSets.main
          source project(":common").sourceSets.main
        }
      }
    }
  }
}

dependencies {
  minecraft "net.minecraftforge:forge:${minecraft_version}-${forge_version}"
  annotationProcessor("org.spongepowered:mixin:0.8.5:processor")

  /*
    Hack fix for now, force jopt-simple to be exactly 5.0.4 because Mojang ships that version,
    but some transitive dependencies request 6.0+
  */
  implementation("net.sf.jopt-simple:jopt-simple:${forge_jopt_version}") {
    version { strictly "${forge_jopt_version}" }
  }
}

mixin {
  add(sourceSets.main, "${mod_id}.refmap.json")

  config("${mod_id}.mixins.json")
  config("${mod_id}.forge.mixins.json")
}

afterEvaluate {
  publishing {
    publications {
      mavenJava(MavenPublication) {
        fg.component(it)
      }
    }
  }
}

sourceSets.each {
  def dir = layout.buildDirectory.dir("sourcesSets/$it.name")
  it.output.resourcesDir = dir
  it.java.destinationDirectory = dir
}
